buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['Teleport_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Teleport_" + name]).toInteger()
}

android {
  namespace "com.teleport"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

afterEvaluate {
  def cfg =
    configurations.findByName("debugCompileClasspath") ?:
    configurations.findByName("releaseCompileClasspath")

  def comp = cfg.incoming.resolutionResult.allComponents.find { c ->
    def id = c.moduleVersion
    id != null && id.group == "com.facebook.react" && id.name == "react-android"
  }

  def rnVersion = comp?.moduleVersion?.version ?: "0.0.0"
  def rnMinor = rnVersion == "0.0.0" ? 0 : (rnVersion.tokenize('.')[1] as int)

  android.sourceSets.main.java.srcDirs += (rnMinor <= 80 ? "src/lt081/java" : "src/gte081/java")
  println("Teleport source set selected for RN ${rnVersion}: " + (rnMinor <= 80 ? "Pre RN80" : "Post RN80"))
}
